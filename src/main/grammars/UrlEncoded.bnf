{
  parserClass="com.linuxgods.kreiger.intellij.idea.url.encoded.parser.UrlEncodedParser"
  parserImports="static com.intellij.psi.TokenType.WHITE_SPACE, static com.intellij.psi.TokenType.BAD_CHARACTER"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="UrlEncoded"
  psiImplClassSuffix="Impl"
  psiPackage="com.linuxgods.kreiger.intellij.idea.url.encoded.psi"
  psiImplPackage="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.impl"

  elementTypeHolderClass="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.UrlEncodedTypes"
  elementTypeClass="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.UrlEncodedElementType"
  tokenTypeClass="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.UrlEncodedTokenType"

/*
  tokens = [
    AMPERSAND       = '&'
    EQUALS          = '='
    WHITE_SPACE     = 'regexp:\R'
    UNESCAPED_NAME  = 'regexp:[^&=%+\s]+'
    UNESCAPED_VALUE = 'regexp:[^&=%+\s]+'
    VALID_ESCAPE    = 'regexp:\+|%[0-9a-fA-F]{2}'
    INVALID_ESCAPE  = 'regexp:%[^0-9a-fA-F]?'
  ]
 */

}

urlEncodedFormData ::= QUESTION_MARK? query_string

private query_string ::= AMPERSAND* fields AMPERSAND*

private fields ::= field_ (AMPERSAND | WHITE_SPACE)+ fields | field_

private field_ ::= field { recoverWhile="field_recover" pin=1 }

field ::= field_name optional_value? {
    implements="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.impl.UrlEncodedNamedElement"
    mixin="com.linuxgods.kreiger.intellij.idea.url.encoded.psi.impl.UrlEncodedFieldMixin"
}

private optional_value ::= EQUALS field_value

field_name ::= string_part+ { extends=string }

field_value ::= string_part* { extends=string }

private string_part ::= unreserved | escaped | unescaped

fake string ::= string_part*

unreserved ::= UNRESERVED_TOKEN
escaped ::= VALID_ESCAPE+
unescaped ::= BAD_CHARACTER

private invalid_escape_ ::= INVALID_ESCAPE

private field_recover ::= !( AMPERSAND | WHITE_SPACE )
